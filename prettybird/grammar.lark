?start: (function_definition | character)+

function_definition: "define" IDENTIFIER function_parameter_list steps_statements

// Was concerned about parameter lists like in "define test(a, b, c,)" but python allows trailing commas
// so we should allow it too
function_parameter_list: "(" [function_parameters] ")"
function_parameters: CNAME "," function_parameters
                   | CNAME
    
character: "char" IDENTIFIER ("encoding=" INT)? "{" "base" "{" base_statement "}" "steps" steps_statements "}"

?base_statement: blank_statement
               | constant_base_statement
               | from_character_base_statement

blank_statement: "blank" "(" INT "," INT ")"

constant_base_statement: CONSTANT_BASE_STATEMENT_CHARACTER+ "," constant_base_statement
                       | CONSTANT_BASE_STATEMENT_CHARACTER+ ","
                       | CONSTANT_BASE_STATEMENT_CHARACTER+

from_character_base_statement: "from_char" "(" IDENTIFIER ")"

steps_statements: "{" "}"
                | "{" step_statement+ "}"

step_statement: UPDATE_MODE FILLED? HALF? step
              | UPDATE_MODE HALF? FILLED? step
              | function_call_step

?step: point_step
     | vector_step
     | circle_step
     | ellipse_step
     | square_step
     | rectangle_step

// Just this point
point_step: "point" "(" point_type ")"

// Line from first point to second point
vector_step: "vector" "(" point_type "," point_type ")"

// Circle with center at point and with radius NUM
circle_step: "circle" "(" point_type "," number_type ")"

// Ellipse with center at point, width NUM, height NUM
// or an ellipse within the rectangle defined by bottomleft=point, topright=point
ellipse_step: "ellipse" "(" point_type "," number_type "," number_type ")"
            | "ellipse" "(" point_type "," point_type ")"

// Square with top left at point and side length NUM
square_step: "square" "(" point_type "," number_type ")"

// Rectangle with top left at point, width NUM, height NUM
rectangle_step: "rectangle" "(" point_type "," number_type "," number_type ")"

function_call_step: CNAME "(" [function_call_parameters] ")"
function_call_parameters: type "," function_call_parameters
                        | type

point: "(" number_type "," number_type ")"

?type: point_type | number_type

?point_type: point | CNAME

?number_type: (NUMBER | CNAME) -> positive_int
            | "-" (NUMBER | CNAME) -> negative_int

UPDATE_MODE: ("draw" | "erase")

FILLED: "filled"

HALF: "half" "(" DIRECTION ")"

DIRECTION: ["top" | "bottom" | "left" | "right"]

CONSTANT_BASE_STATEMENT_CHARACTER: ("0" | ".")

IDENTIFIER: CNAME | DIGIT

SINGLE_LINE_COMMENT: "//" /[^\n]*/ "\n"

WHITESPACE: /\s+/

%ignore SINGLE_LINE_COMMENT
%ignore WHITESPACE
%ignore C_COMMENT

%import common.CNAME
%import common.INT
%import common.NUMBER
%import common.C_COMMENT
%import common.DIGIT
